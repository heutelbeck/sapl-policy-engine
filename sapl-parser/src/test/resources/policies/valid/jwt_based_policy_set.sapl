/*
 * This set of demo policies illustrates how JWT can be used in tandem with SAPL.
 *
 * The JWT extensions for SAPL enable access to data contained in a JWT as attributes.
 * In addition, the JWT PIP allows for token validation and time-based policies
 * dynamically taking into account the time for which a JWT is valid.
 */

set "JWT Demo Policies"
first or abstain errors propagate

/*
 * The spring security libraries for OAuth2 JWT integration map each element of
 * the "scope" array in an JWT token to a Spring Principal authority, prepending
 * "SCOPE_" to each String representing the scope.
 * These can be references as any "ROLE_" or other authority provided by Spring
 * security.
 */
policy "Scopes as Authority in Principal"
permit
    resource == "books";
	"SCOPE_books.read" in subject..authority;

/*
 * Read JWT claims directly via the JWT PIP environment attribute.
 * The token is read securely from subscription secrets.
 */
policy "Reading scopes from JWT via PIP"
permit
    resource == "faculty";
	"faculty.read" in <jwt.token>.payload.scope;

/*
 * Read JWT claims from the PIP. The token is securely stored
 * in subscription secrets and accessed via the JWT PIP.
 */
policy "Reading scopes from JWT PIP attribute"
permit
    resource == "bestiary";
    "bestiary.read" in <jwt.token>.payload.scope;

/*
 * This policy will evaluate to permit for the time the token is valid
 */
policy "Policy to test timeout of token"
permit
    resource == "mysteries";
	<jwt.token>.valid;
