/*
 * Copyright (C) 2017-2023 Dominic Heutelbeck (dominic@heutelbeck.com)
 *
 * SPDX-License-Identifier: Apache-2.0
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
module io.sapl.test.grammar.GenerateSAPLTest

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import io.sapl.generator.web.*

var rootPath = ".."

Workflow {

    bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
        scanClassPath = true
        platformUri = rootPath
    }

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "sapl-test-lang"
				rootPath = rootPath
                genericIde = {
       				name = "sapl-test-ide"
   				}
				runtimeTest = {
					enabled = false
				}
                web = {
					name = "sapl-test-web"
					enabled = true
					overwriteSrc = true
					assets = "${rootPath}/sapl-test-web/src/main/resources/static"
				}
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * SAPLTest generated by Xtext\n */"
				preferXtendStubs = false
			}
		}

		cleaner = {
            enabled = false
        }

		language = StandardLanguage {
			name = "io.sapl.test.grammar.SAPLTest"
			fileExtensions = "sapltest"

			referencedResource = "platform:/resource/sapl-test-lang/src/main/model/custom/SAPLTest.genmodel"

            fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			junitSupport = {
				junitVersion = "5"
			}
            webSupport = SAPLWebIntegrationFragment {
                generateHtmlExample = false
                codeMirrorVersion = "5.39.2"
//				aceVersion = "1.2.6"
                requireJsVersion = "2.2.0"
                requireJsTextVersion = "2.0.15"
                jQueryVersion = "2.2.3"
                framework = "CodeMirror" // Orion, CodeMirror, Ace
                generateJettyLauncher = false
                generateJsHighlighting = true // overwrites file on every run
                keywordsFilter = "[a-zA-Z0-9_-]+"
                generateServlet = false
                generateWebXml = false
                highlightingModuleName ="sapl-test-mode"
                // some patterns need to be suppressed to make Bracket Matching working
                suppressPattern = "bracket_open"
                suppressPattern = "parenthesis_open"
                suppressPattern = "brace_open"
                suppressPattern = "bracket_close"
                suppressPattern = "parenthesis_close"
                suppressPattern = "brace_close"
            }
		}
	}
}