set "updatePolicies"
deny-unless-permit
for action in ["update", "updateDiagnosis"]

policy "permit_admin_and_doctor_update_profile"
permit
  action == "update"
where
  "ADMIN" in subject..authority || "DOCTOR" in subject..authority;
  resource == "profile";

policy "permit_nurse_update_profile"
permit
  action == "update"
where
  "NURSE" in subject..authority;
  resource == "profile";


policy "permit_admin_update_name_hrn_phoneNumber_roomNumber"
permit
   action == "update"
where
   "ADMIN" in subject..authority;
   resource in ["name", "HRN", "phoneNumber", "roomNumber"];


policy "permit_attending_doctor_update_diagnosis"
permit
   action == "updateDiagnosis"
where
   subject.name == resource.attendingDoctor;
obligation
  {
    "type" : "sendEmail",
    "recipient" : "supervisor@example.org",
    "subject" : "Diagnosis update by doctor",
    "message" : subject.name + " has updated the diagnosis of " + resource.name
  }


policy "permit_doctor_update_attending_doctor"
permit
   action == "update"
where
   "DOCTOR" in subject..authority;
   resource == "attendingDoctor";


policy "permit_doctor_and_nurse_update_attending_nurse"
permit
   action == "update"
where
   "DOCTOR" in subject..authority || "NURSE" in subject..authority;
   resource == "attendingNurse";
