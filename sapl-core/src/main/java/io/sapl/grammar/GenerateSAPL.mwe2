/**
 * Copyright Â© 2017 Dominic Heutelbeck (dheutelbeck@ftk.de)
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
module io.sapl.grammar.GenerateSAPL

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
 
Workflow { 

	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
        scanClassPath = true
        platformUri = rootPath
    }

	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "sapl-core"
				rootPath = rootPath
				genericIde = {
       				name = "sapl-ide"
   				} 
				runtimeTest = {
					enabled = true
				}
				web = {
					name = "sapl-web"			
					enabled = true
					overwriteSrc = true
					assets = "${rootPath}/sapl-web/src/main/resources/static"
					
				}
				mavenLayout = true
			}
			code = {
				encoding = "UTF-8"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
			}
		}
		
		cleaner = {
			enabled = false
		}
		
		language = StandardLanguage {
			name = "io.sapl.grammar.SAPL"
			fileExtensions = "sapl"
			
			referencedResource = "platform:/resource/sapl-core/src/main/model/custom/SAPL.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
			}
			//parserGenerator = {
				//debugGrammar = true
			//}

			webSupport = {
				generateHtmlExample = false
				codeMirrorVersion = "5.39.2"
//				aceVersion = "1.2.6"
				requireJsVersion = "2.2.0"
				requireJsTextVersion = "2.0.15"
				jQueryVersion = "2.2.3"
				framework = "CodeMirror" // Orion, CodeMirror, Ace
				generateJettyLauncher = false
				generateJsHighlighting = true // overwrites file on every run
				generateServlet = false
				generateWebXml = false
				highlightingModuleName ="SaplJSHighlighting"
				// some patterns need to be suppressed to make Bracket Matching working
				suppressPattern = "bracket_open"
				suppressPattern = "parenthesis_open"
				suppressPattern = "brace_open"
				suppressPattern = "bracket_close"
				suppressPattern = "parenthesis_close"
				suppressPattern = "brace_close"
			}
		}
	}
}
